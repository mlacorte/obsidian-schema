@top MarkdownDoc { (schemaOpen Schema schemaClose | Markdown)* }
@top SchemaDoc { block }

Markdown { unknown }
Schema { block }

block { listOf<Property> }

expression {
  value |
  postfixExpression |
  mathExpression |
  comparisonExpression |
  logicalExpression  |
  Not { "!" expression } |
  "(" expression ~lambdaOrParen ")"
}

value {
  Identifier |
  Bool |
  Null |
  Number |
  String |
  Link |
  EmbedLink |
  Tag |
  Date |
  Duration |
  Object |
  Array |
  Lambda
}

@precedence { postFix @left, mulDiv @left, plsMin @left, cmp @left, logic @left }

postfixExpression {
  Call { expression !postFix "(" listOf<expression> ")" } |
  Index { expression !postFix "[" expression "]" } |
  Dot { expression !postFix "." Identifier }
}

mathExpression {
  Multiply { expression !mulDiv "*" expression } |
  Divide { expression !mulDiv "/" expression } |
  Plus { expression !plsMin "+" expression } |
  Minus { expression !plsMin "-" expression }
}

comparisonExpression {
  Gte { expression !cmp ">=" expression } |
  Lte { expression !cmp "<=" expression } |
  Neq { expression !cmp "!=" expression } |
  Gt { expression !cmp ">" expression } |
  Lt { expression !cmp "<" expression } |
  Eq { expression !cmp "=" expression }
}

logicalExpression {
  And { expression !logic ("and" | "&") expression } |
  Or { expression !logic ("or" | "|") expression }
}

Identifier { identifier }
Date { "date(" ( DateLiteral | DateShorthand) ")" }
Duration { "dur(" durationPair (","? durationPair)* ")" }

Lambda { "(" LambdaArgs ~lambdaOrParen ")" "=>" LambdaExpr }
LambdaArgs { listOf<Arg | TypedArg> }
LambdaExpr { expression }
Arg { Identifier }
TypedArg { Identifier ":" expression }

Array  { "[" listOf<expression> "]" }
Object { "{" listOf<Property> "}" }
Property { (String | Tag | Link | Identifier) ":" expression }

durationPair {
  DurationYears { Number ("year" | "years" | "yr" | "yrs") } |
  DurationMonths { Number ("month" | "months" | "mo" | "mos") } |
  DurationWeeks { Number ("week" | "weeks" | "wk" | "wks" | "w") } |
  DurationDays { Number ("day" | "days" | "d") } |
  DurationHours { Number ("hour" | "hours" | "hr" | "hrs" | "h") } |
  DurationMinutes { Number ("minute" | "minutes" | "min" | "mins" | "m") } |
  DurationSeconds { Number ("second" | "seconds" | "sec" | "secs" | "s") }
}

@tokens {
  Number { "-"? $[0-9]+ ("." $[0-9]+)? }
  String { '"' ("\\" _ | !["\\])* '"' }
  Bool { "true" | "false" | "True" | "False" }
  Link { "[[" ![\[\]]* "]]" }
  EmbedLink { "![[" ![\[\]]* "]]" }
  Null { "null" }
  Tag { "#" ![\u2000-\u206F\u2E00-\u2E7F'!"#$%&()*+,.:;<=>?@^`{|}~\[\]\\ \t\r\n\v\f]* }
  DateShorthand {
    "now" | "today" | "yesterday" | "tomorrow" |
    "sow" | "eow" | "soy" | "eoy" | "som" | "eom"
  }
  DateLiteral {
    @digit @digit @digit @digit "-" @digit @digit (
      "-" @digit @digit (
      "T" @digit @digit (
      ":" @digit @digit (
      ":" @digit @digit (
      "." @digit @digit @digit
    )?)?)?)?)?
  }
  whitespace { @whitespace }
}

@external tokens schemaOpenTokenizer from "./schema.tokens" { schemaOpen }
@external tokens schemaCloseTokenizer from "./schema.tokens" { schemaClose }
@external tokens unknownTokenizer from "./schema.tokens" { unknown }
@external tokens identifierTokenizer from "./schema.tokens" { identifier }

@skip { whitespace }
listOf<item> { (item ("," item)* ","?)? }

@detectDelim