@top Document { (Markdown | SchemaBlock | DataBlock)* }

Markdown { unknown }

Schema { Block }
SchemaBlock { schemaDelim Schema schemaDelim }

Data { Block }
DataBlock { dataDelim Data dataDelim }

Block { value }

value { True | False | Null | Number | String | Object | Array }

String { string }
Object { "{" list<Property>? "}" }
Array  { "[" list<value>? "]" }

Property { PropertyName ":" value }
PropertyName { string }

@tokens {
  True  { "true" }
  False { "false" }
  Null  { "null" }

  Number { '-'? int frac? exp?  }
  int  { '0' | $[1-9] @digit* }
  frac { '.' @digit+ }
  exp  { $[eE] $[+\-]? @digit+ }

  string { '"' char* '"' }
  char { $[\u{20}\u{21}\u{23}-\u{5b}\u{5d}-\u{10ffff}] | "\\" esc }
  esc  { $["\\\/bfnrt] | "u" hex hex hex hex }
  hex  { $[0-9a-fA-F] }

  whitespace { $[ \n\r\t] }
}

@external tokens schemaDelimTokenizer from "./schema.tokens" { schemaDelim }
@external tokens dataDelimTokenizer from "./schema.tokens" { dataDelim }
@external tokens unknownTokenizer from "./schema.tokens" { unknown }

@skip { whitespace }
list<item> { item ("," item)* }

@detectDelim